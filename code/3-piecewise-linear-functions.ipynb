{
 "cells": [
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "**Description**: Shows how to compare MIP formulations for transportation problems with piecewise linear objective functions.\n",
    "\n",
    "**Author**: Juan Pablo Vielma\n",
    "\n",
    "**License**: <a rel=\"license\" href=\"http://creativecommons.org/licenses/by-sa/4.0/\"><img alt=\"Creative Commons License\" style=\"border-width:0\" src=\"https://i.creativecommons.org/l/by-sa/4.0/88x31.png\" /></a><br />This work is licensed under a <a rel=\"license\" href=\"http://creativecommons.org/licenses/by-sa/4.0/\">Creative Commons Attribution-ShareAlike 4.0 International License</a>."
   ]
  },
  {
   "cell_type": "markdown",
   "metadata": {},
   "source": [
    "# MIP formulations for piecewise linear functions  with JuMP\n",
    "\n",
    "We consider the transportation problem\n",
    "$$\\begin{alignedat}{3}\n",
    " &\\min & \\quad  \\sum_{s=1}^{n_s} \\sum_{d=1}^{n_d} f_{s,d}(x_{s,d})  \\\\\n",
    " \\notag&s.t.\\\\\n",
    " &      &  \\sum_{d=1}^{n_d} x_{s,d} &= supply_s,  &\\quad& \\forall s\\in \\{1,\\ldots,n_s\\} \\\\\n",
    "  &      &   \\sum_{s=1}^{n_s} x_{s,d} &= demand_d,  &\\quad& \\forall d\\in \\{1,\\ldots,n_d\\} \\\\\n",
    "  &      &  x_{s,d} &\\geq 0,  &\\quad& \\forall s\\in \\{1,\\ldots,n_s\\},\\quad d\\in \\{1,\\ldots,n_d\\} \n",
    " \\end{alignedat}$$\n",
    " where $ f_{s,d}:\\mathbb{R}\\to \\mathbb{R}$ is a piecewise linear function. \n",
    "\n",
    "We begin constructing a Linear Programming formulation for the case in which all $ f_{s,d}$ are linear and then consider Mixed Integer Programming formulations for the general case. "
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using JuMP, Cbc"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Create JuMP model, using Cbc as the solver\n",
    "model = Model(solver=CbcSolver())\n",
    "\n",
    "# Data\n",
    "m = n = 3\n",
    "S = 1:m\n",
    "D = 1:n\n",
    "supply = rand(1:10, m)\n",
    "demand = rand(1:10, n)\n",
    "demand_imbalance = sum(demand) - sum(supply)\n",
    "if demand_imbalance > 0\n",
    "    supply[m] += demand_imbalance\n",
    "else\n",
    "    demand[n] -= demand_imbalance\n",
    "end    \n",
    "c = rand(m,n)\n",
    "\n",
    "# Define variables\n",
    "@variable(model, x[S,D] >= 0)\n",
    "\n",
    "# Add constraints\n",
    "@constraints(model, begin    \n",
    "    supply_constraints[s=S], sum(x[s,d] for d in D) == supply[s]\n",
    "    demand_constraints[d=D], sum(x[s,d] for s in S) == demand[d]    \n",
    "end)\n",
    "\n",
    "# Define objective\n",
    "@objective(model, Min, sum(c[s,d]*x[s,d] for  s in S, d in D))\n",
    "\n",
    "# Display model\n",
    "println(model)\n",
    "\n",
    "# Solve Model\n",
    "println(\"Solving...\")\n",
    "status = solve(model)\n",
    "\n",
    "# Display results\n",
    "println(\"Solver status: \", status)\n",
    "println(\"Cost: \", getobjectivevalue(model))\n",
    "println(\"Solution: \\n\",getvalue(x))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate supply, demand and piecewise linear objective\n",
    "function generateData(m, n, num_segments)\n",
    "    \n",
    "#     seed = MersenneTwister(hash((num_segments,m,n)))\n",
    "    \n",
    "    S = 1:m\n",
    "    D = 1:n\n",
    "#     supply = rand(seed, 1:10, m)\n",
    "#     demand = rand(seed, 1:10, n)\n",
    "    supply = rand(1:10, m)\n",
    "    demand = rand(1:10, n)\n",
    "    \n",
    "    demand_imbalance = sum(demand) - sum(supply)\n",
    "    if demand_imbalance > 0\n",
    "        supply[m] += demand_imbalance\n",
    "    else\n",
    "        demand[n] -= demand_imbalance\n",
    "    end\n",
    "    \n",
    "    fvalues = [zeros(num_segments+1) for s in S, d in D]\n",
    "    dvalues = [zeros(num_segments+1) for s in S, d in D]\n",
    "    for s in S, d in D\n",
    "        drange = min(supply[s],demand[d])\n",
    "        Δ = drange / num_segments\n",
    "        dvalues[s,d] = [(i-1)*Δ for i=1:(num_segments+1)]\n",
    "            \n",
    "        slopes = sort(rand(num_segments), rev=true)\n",
    "        for i in 2:(num_segments+1)\n",
    "            fvalues[s,d][i] = fvalues[s,d][i-1] + slopes[i-1]*(dvalues[s,d][i]-dvalues[s,d][i-1])\n",
    "        end\n",
    "    end\n",
    "    \n",
    "    supply, demand, fvalues, dvalues\n",
    "end\n",
    "\n",
    "supply, demand, fvalues, dvalues = generateData(1,1,8)\n",
    "snodes = length(supply); dnodes = length(demand);"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Plot a piecewise linear objective\n",
    "using Plots\n",
    "plot(dvalues[1,1],fvalues[1,1], legend=false)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {
    "scrolled": true
   },
   "outputs": [],
   "source": [
    "using PiecewiseLinearOpt\n",
    "\n",
    "# Generate Data\n",
    "m = 1\n",
    "n = 2\n",
    "S = 1:m\n",
    "D = 1:n\n",
    "supply, demand, fvalues, dvalues = generateData(m,n,3)\n",
    "\n",
    "# Create JuMP model, using Cbc as the solver\n",
    "model = Model(solver=CbcSolver())\n",
    "\n",
    "# Define variables\n",
    "@variable(model, x[S,D] >= 0)\n",
    "\n",
    "# Add constraints\n",
    "@constraints(model, begin    \n",
    "    supply_constraints[s=S], sum(x[s,d] for d in D) == supply[s]\n",
    "    demand_constraints[d=D], sum(x[s,d] for s in S) == demand[d]    \n",
    "end)\n",
    "\n",
    "# Define objective\n",
    "@objective(model, Min, sum(piecewiselinear(model, x[s,d], dvalues[s,d], fvalues[s,d]) for s in S, d in D))\n",
    "\n",
    "# Display Model\n",
    "println(model)\n",
    "\n",
    "# Solve Model\n",
    "println(\"Solving...\")\n",
    "status = solve(model)\n",
    "\n",
    "# Display results\n",
    "println(\"Solver status: \", status)\n",
    "println(\"Cost: \", getobjectivevalue(model))\n",
    "println(\"Solution: \\n\",getvalue(x))"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "function solveModel(supply, demand, fvalues, dvalues, formulation, solver=CbcSolver())\n",
    "    S = 1:length(supply)\n",
    "    D = 1:length(demand)\n",
    "    model = Model(solver=solver)\n",
    "\n",
    "    @variable(model, x[S,D] >= 0)\n",
    "\n",
    "    # Add constraints\n",
    "    @constraints(model, begin    \n",
    "        supply_constraints[s=S], sum(x[s,d] for d in D) == supply[s]\n",
    "        demand_constraints[d=D], sum(x[s,d] for s in S) == demand[d]    \n",
    "    end)\n",
    "\n",
    "    obj = sum(piecewiselinear(model, x[s,d], dvalues[s,d], fvalues[s,d]; method=formulation) for s in S, d in D)\n",
    "    @objective(model, Min, obj)\n",
    "    \n",
    "    println(\"Solving for \", formulation, \" ...\")\n",
    "    tic()\n",
    "    status = solve(model)\n",
    "    solvetime = toq();\n",
    "    println(\"Solver status: \", status)\n",
    "    println(\"Cost: \", getobjectivevalue(model))\n",
    "    println(\"Solve time: \", solvetime)\n",
    "    println()\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "# Generate Data\n",
    "supply, demand, fvalues, dvalues = generateData(10, 10, 30)\n",
    "\n",
    "formulations = [:CC,:MC,:SOS2,:Incremental,:Logarithmic,:DisaggLogarithmic,:ZigZag,:ZigZagInteger]\n",
    "\n",
    "for formulation in formulations\n",
    "    solveModel(supply, demand, fvalues, dvalues, formulation, CbcSolver(logLevel=0, seconds=60.))\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "metadata": {},
   "outputs": [],
   "source": [
    "using Gurobi\n",
    "\n",
    "for formulation in formulations\n",
    "    solveModel(supply, demand, fvalues, dvalues, formulation,GurobiSolver(TimeLimit=60,OutputFlag=0))\n",
    "end"
   ]
  }
 ],
 "metadata": {
  "celltoolbar": "Raw Cell Format",
  "kernelspec": {
   "display_name": "Julia 0.6.2",
   "language": "julia",
   "name": "julia-0.6"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "0.6.2"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 1
}
